#+TITLE: Emacs configuration file
#+AUTHOR: FunLexer
#+BABEL: :cache yes
#+PROPERTY: header-args :tangle yes
#+OPTIONS: toc:3 num:nil ^:nil \n:t

* Config
** Meta

#+begin_src emacs-lisp
;;; -*- lexical-binding: t -*-
#+end_src

** Functions

<<sec:defuns>>

#+begin_src emacs-lisp
(defun my/kill-this-buffer-unless-scratch ()
  "*scratch* 버퍼는 삭제하지 않고, 버퍼 내용을 삭제"
  (interactive)
  (if (not (string= (buffer-name) "*scratch*"))
      (kill-this-buffer)
    (delete-region (point-min) (point-max))
    (switch-to-buffer (other-buffer))
    (bury-buffer "*scratch*")))

(add-hook 'kill-buffer-query-functions
          (lambda() (not (equal (buffer-name) "*scratch*"))))

;; |01234567|
;; |abcdefgh|
;; |ABCDEFGH|
;; |--------|
;; |일바이트|
(defun my/set-font (font size)
  "폰트 변경"
  (interactive
   (let ((completion-ignore-case  t))
     (list (completing-read "Font: " (font-family-list) nil t)
           (read-number "Size: "))))
  (when (member font (font-family-list))
    (set-face-font 'default (font-spec :family font :size size))))

;; https://www.blogbyben.com/2022/05/gotcha-emacs-on-mac-os-too-many-files.html
(defun my/file-notify-rm-all-watches ()
  "Emacs에서 알림 감시 제거. Too many open files 오류 발생 시 수행"
  (interactive)
  (maphash
   (lambda (key _value)
     (file-notify-rm-watch key))
   file-notify-descriptors))

(defun my/minibuffer-backward-kill-word (arg)
  "미니버퍼에서 경로(단어)마다 삭제"
  (interactive "p")
  (if minibuffer-completing-file-name
      ;; Borrowed from https://github.com/raxod502/selectrum/issues/498#issuecomment-803283608
      (if (string-match-p "/." (minibuffer-contents))
          (zap-up-to-char (- arg) ?/)
        (delete-minibuffer-contents))
    (delete-backward-char arg)))

(defun my/cred (address ports user)
  "등록된 인증서 정보를 읽는다."
  (let* ((found (nth 0 (auth-source-search :max 1
                                           :host address
                                           :port ports
                                           :user user
                                           :require '(:user :secret)))))
    (if found
        (list (plist-get found :user)
              (let ((secret (plist-get found :secret)))
                (if (functionp secret)
                    (funcall secret)
                  secret)))
      nil)))

(defun my/join-path (root &rest dirs)
  "파일 경로 도움 함수
Examples: (my/join-path \"/tmp\" \"a\" \"b.txt\") => /tmp/a/b.txt"
  (if (not dirs)
      root
    (apply 'my/join-path
           (expand-file-name (car dirs) root)
           (cdr dirs))))

(defun my/read-file (path)
  "파일 컨텐츠 읽기"
  (with-temp-buffer
    (insert-file-contents path)
    (buffer-string)))

(defun my/enable-line-numbers-mode ()
  "버퍼 라인 번호 표시 활성화"
  (interactive)
  (display-line-numbers-mode 1))

(defun my/disable-line-numbers-mode ()
  "버퍼 라인 번호 표시 비활성화"
  (interactive)
  (display-line-numbers-mode -1))

(defun my/org-align-all-tags ()
  "Org mode에서 버퍼의 tag 정렬"
  (interactive)
  ;; '(4)는 C-u와 동일
  (org-set-tags-command '(4)))

;; 현재의 테마를 비활성화하고 로딩하도록 설정한다.
(defun my/disable-themes-before-load (theme &optional no-confirm no-enable)
  (mapc #'disable-theme custom-enabled-themes))
(advice-add 'load-theme
            :before #'my/disable-themes-before-load)
#+end_src

** Better defaults

#+begin_src emacs-lisp
(setq-default
 auto-revert-interval 1                        ;; 버퍼를 빠르게 새로고침
 warning-minimum-level :emergency              ;; 네이티브 컴파일 경고 끄기
 system-time-locale "C"                        ;; 시스템 로케일
 echo-keystrokes 0.1                           ;; 키 입력 즉시 표시
 inhibit-startup-screen t                      ;; 시작 스플래시 제거
 initial-scratch-message nil                   ;; scratch 버퍼 비우기
 ring-bell-function 'ignore                    ;; 경고음 끄기
 scroll-margin 1                               ;; 스크롤 여백
 sentence-end-double-space nil                 ;; 문장 끝 두칸 공백 비활성화
 vc-handled-backends nil                       ;; 기본 VCS 무시 (magit 사용)
 ad-redefinition-action 'accept                ;; ad-handle-definition 경고 무시
 display-time-default-load-average nil         ;; load average 표시 안함
 default-input-method "korean-hangul"          ;; 기본 입력기
 tab-width 2                                   ;; 탭 폭 2
 sh-basic-offset 2                             ;; Shell 기본 들여쓰기
 c-basic-offset 2                              ;; C 계열 기본 들여쓰기
 fill-column 79                                ;; 최대 줄 너비
 truncate-lines t                              ;; 줄 개행 안함
 indent-tabs-mode nil                          ;; Tab 대신 space 사용
 split-width-threshold 160                     ;; 기본 수직 분할 기준
 split-height-threshold nil                    ;; 기본 수직 분할 기준
 frame-resize-pixelwise t                      ;; 프레임 픽셀 단위 조절
 use-short-answers t                           ;; y/n 사용
 find-file-visit-truename nil                  ;; 심볼릭 링크를 그대로 유지하도록 설정
)


;; 키 입력 명령 필터
(setq read-extended-command-predicate #'command-completion-default-include-p)

;; 백업 파일을 단일 디렉토리에 저장
(let ((dir (expand-file-name "auto-save-list" user-emacs-directory)))
  (setq backup-directory-alist `((".*" . ,dir))
        auto-save-file-name-transforms `((".*" ,dir t))))

;; 경고음 끄기
(put 'narrow-to-region 'disabled -1)

;; 일반 메시지모드에서도 org 테이블 사용
(add-hook 'message-mode-hook 'turn-on-orgtbl)

;; 디스크 파일 변경 시, 자동 새로고침
(add-hook 'doc-view-mode-hook 'auto-revert-mode)

;; whitespace 정리
(add-hook 'before-save-hook 'delete-trailing-whitespace)

(when (eq system-type 'darwin)
  (setq ns-pop-up-frames nil
        mac-option-modifier nil
        mac-command-modifier 'meta)
  (when (boundp 'mac-pass-command-to-system)
    (setq mac-pass-command-to-system nil))
  (when (fboundp 'mac-auto-operator-composition-mode)
    (mac-auto-operator-composition-mode t)))

;; hook/mode/display 관련 설정
(use-package emacs
  :straight (:type built-in)
  :custom
  (display-time-format "%Y-%m-%dT%H:%M")
  (tab-always-indent 'complete)
  :config
  (global-display-line-numbers-mode 1)
  (display-time-mode 1)
  (dirtrack-mode 1)
  (blink-cursor-mode -1)
  (menu-bar-mode -1)
  (tool-bar-mode -1)
  (scroll-bar-mode -1))

(use-package recentf
  :straight (:type built-in)
  :custom
  (recentf-max-saved-items 100)
  (recentf-exclude '("/var/folders/*" "^/tmp/*"))
  :config
  (recentf-mode 1))

(use-package paren
  :straight (:type built-in)
  :config
  ;; 괄호 강조
  (show-paren-mode 1))

(use-package simple
  :straight (:type built-in)
  :config
  (delete-selection-mode 1)
  (column-number-mode 1))

(use-package so-long
  :disabled
  :straight (:type built-in)
  :config
  (global-so-long-mode 1))

(use-package xref
  :straight (:type built-in))

(use-package project
  :straight (:type built-in))

(use-package ansi-color
  :straight (:type built-in)
  :config
  (defun my/ansi-colorize-buffer ()
    "현재 버퍼에 ANSI 색상 표시"
    (interactive)
    (ansi-color-apply-on-region (point-min) (point-max))))

(use-package ibuffer
  :straight (:type built-in)
  :init
  (defalias 'list-buffers 'ibuffer)
  :hook
  ((ibuffer-mode . ibuffer-auto-mode))
  :config
  (setq ibuffer-expert t
        ibuffer-default-sorting-mode 'major-mode))
#+end_src

** Base Packages

#+begin_src emacs-lisp
;; Emacs Lisp 라이브러리 컴파일러
(use-package auto-compile)
;; 바이너리 컴파일
(use-package cmake-mode)
;; 사용가능한 키 바인딩 표시
(use-package which-key
  :config
  (which-key-mode 1))
;; Git 유틸
(use-package magit
  :config
  ;; gpg-sign 추가하도록 구성
  (setq transient-default-level 5))
;; 페이지 나누기(^L) 표시 변경
(use-package page-break-lines)
;; 툴팁
(use-package popup)
;; decorations to HTML
(use-package htmlize)
;; 들여쓰기 표시
(use-package highlight-indentation)
;; RFC
(use-package rfc-mode)
;; 멀티 커서
(use-package multiple-cursors)
;; 괄호 편집 유틸
(use-package paredit)
;; 스니펫 모음
(use-package yasnippet-snippets
  :config
  (yas-global-mode 1))
;; 선택 영역 늘리고 줄이는 유틸
(use-package expand-region)

(use-package reveal-in-osx-finder
  :if (eq system-type 'drawin))
#+end_src

** Environment

#+begin_src emacs-lisp
(use-package exec-path-from-shell
  :when (memq window-system '(mac ns x pgtk))
  :custom
  (exec-path-from-shell-variables '("PATH" "TMPDIR" "GOROOT" "GOPATH" "JAVA_HOME"))
  :hook
  ((after-init . exec-path-from-shell-initialize)))
#+end_src

** Undo

#+begin_src emacs-lisp
(use-package undo-tree
  :custom
  (undo-tree-visualizer-diff t)
  (undo-tree-visualizer-timestamps t)
  (undo-tree-history-directory-alist '(("." . "~/.emacs.d/undo")))
  :config
  (global-undo-tree-mode 1))
#+end_src

** Olivetti

#+begin_src emacs-lisp
(use-package olivetti
  :init
  (setq-default olivetti-body-width 82)
  :hook
  ((olivetti-mode-on . my/disable-line-numbers-mode)
   (olivetti-mode-off . my/enable-line-numbers-mode))
  :config
  (remove-hook 'olivetti-mode-on-hook 'visual-line-mode))
#+end_src

** Evil

#+begin_src emacs-lisp
(use-package evil
  :init
  (setq evil-want-integration t
        evil-want-keybinding nil
        evil-undo-system 'undo-tree)
  :config
  (evil-mode 1)

  ;; evil-collection mu4e 1.10 버전 호환성 이슈
  (defun mu4e--main-action-str (str &optional func-or-shortcut))
  (defun evil-collection-mu3e-update-main-view@override())

  (advice-add 'evil-collection-mu3e-update-main-view
              :override #'evil-collection-mu4e-update-main-view@override)

  (evil-ex-define-cmd "q" 'kill-this-buffer)
  (evil-ex-define-cmd "quit" 'evil-quit))

(use-package evil-collection
  :after evil
  :config
  (defun mu4e--main-action-str (str &optional func-or-shortcut))
  (defun evil-collection-mu3e-update-main-view@override())

  (advice-add 'evil-collection-mu3e-update-main-view
              :override #'evil-collection-mu4e-update-main-view@override)
  (evil-collection-init))

(use-package evil-org
  :after (evil org)
  :hook
  ((org-mode . evil-org-mode))
  :config
  (evil-org-set-key-theme '(navigation insert textobjects additional calendar))
  ;; shift+hjkl로 날짜/우선순위/TODO 조절
  (evil-define-key 'normal org-mode-map
    (kbd "H") 'org-shiftleft    ;; shift+h
    (kbd "L") 'org-shiftright   ;; shift+l
    (kbd "K") 'org-shiftup      ;; shift+k
    (kbd "J") 'org-shiftdown))  ;; shift+j

(use-package evil-org-agenda
  :after evil-org
  :straight (:type built-in)
  :config (evil-org-agenda-set-keys))
#+end_src

** IMenu

#+begin_src emacs-lisp
(use-package imemu
  :straight (:type built-in))

(use-package imenu-list
  :custom
  (imenu-list-position 'right)
  (imenu-list-size 0.2)
  (imenu-list-focus-after-activation t)
  (imenu-list-auto-resize t)
  :custom-face
  (imenu-list-entry-face-0 ((t (:inherit font-lock-function-name-face))))
  (imenu-list-entry-face-1 ((t (:inherit font-lock-variable-name-face))))
  (imenu-list-entry-face-2 ((t (:inherit font-lock-type-face))))
  (imenu-list-entry-face-3 ((t (:inherit font-lock-constant-face))))
  (imenu-list-entry-subalist-face-0 ((t (:inherit font-lock-keyword-face :weight bold)))))
#+end_src

** Completion

#+begin_src emacs-lisp
(use-package savehist
  :config
  (savehist-mode 1))

(use-package corfu
  :custom
  (corfu-cycle t)
  (corfu-auto t)
  (corfu-auto-prefix 2)
  (corfu-auto-delay 0)
  (corfu-popupinfo-delay '(0.5 . 0.2))

  :config
  (global-corfu-mode)
  (corfu-popupinfo-mode 1))

(use-package corfu-terminal
  :unless (display-graphic-p)
  :after (corfu)
  :straight ( :type git
              :host nil
              :repo "https://codeberg.org/akib/emacs-corfu-terminal.git")
  :config
  (corfu-terminal-mode 1))

(use-package cape
  :disabled
  :after (corfu)
  :init
  (add-hook 'completion-at-point-functions #'cape-dabbrev)
  (add-hook 'completion-at-point-functions #'cape-file)
  (add-hook 'completion-at-point-functions #'cape-elisp-block))

(use-package vertico
  :init
  ;; (setq vertico-count-format '("%-5s " . "%2$s")
  ;;       vertico-resize nil)
  :config
  (vertico-mode 1))

(use-package consult
  :init
  (setq register-preview-delay 0.5
        register-preview-function #'consult-register-format)
  :config
  (advice-add #'register-preview
              :override #'consult-register-window))

(use-package consult-projectile
  :after (consult projectile)
  :config
  (defcustom consult-projectile-key-bindings
    '((projectile-find-file        . consult-projectile-find-file)
      (projectile-find-dir         . consult-projectile-find-dir)
      (projectile-switch-to-buffer . consult-projectile-switch-to-buffer)
      (projectile-switch-project   . consult-projectile-switch-project)
      (projectile-grep             . consult-grep)
      (projectile-ripgrep          . consult-ripgrep)
      (" "                         . consult-projectile)
      ("si"                        . consult-git-grep)
      ("Oa"                        . consult-org-agenda))
    "Like counsel-projectile-key-bindings"
    :type '(alist :key-type (choice (function :tag "Projectile command")
                                    key-sequence)
                  :value-type (function :tag "Consult-projectile command"))
    :group 'consult-projectile)

  (define-minor-mode consult-projectile-mode
    ""
    :group 'consult-projectile
    :require 'consult-projectile
    :global t
    (cond
     (consult-projectile-mode
      (projectile-mode)
      (dolist (binding consult-projectile-key-bindings)
        (if (functionp (car binding))
            (define-key projectile-mode-map `[remap ,(car binding)] (cdr binding))
          (define-key projectile-command-map (car binding) (cdr binding)))))
     (t
      (dolist (binding consult-projectile-key-bindings)
        (if (functionp (car binding))
            (define-key projectile-mode-map `[remap ,(car binding)] nil)
          (define-key projectile-command-map (car binding) nil)))
      (projectile-mode -1))))
  (consult-projectile-mode 1))

(use-package marginalia
  :config
  (marginalia-mode 1))

(use-package embark)

(use-package embark-consult
  :after (embark consult)
  :hook
  ((embark-collect-mode . consult-preview-at-point-mode)))

(use-package orderless
  :init
  (setq completion-styles '(orderless basic)
        completion-category-defaults nil
        completion-category-overrides '((file (styles partial-completion)))))
#+end_src

** Projectile

#+begin_src emacs-lisp
(use-package projectile
  :init
  (setq projectile-globally-ignored-directories
        '(".git" ".vscode" ".idea" ".svn" ".tox" ".cache" "vendor"))

  (cond
   ;; Windows
   ((eq system-type 'windows-nt)
    nil) ; projectile 기본값 사용

   ;; Linux/Unix
   ((executable-find "rg")
    (setq projectile-generic-command
          (let ((rg-cmd ""))
            (dolist (dir projectile-globally-ignored-directories)
              (setq rg-cmd (format "%s --glob '!%s'" rg-cmd dir)))
            (concat "rg -0 --files --color=never --hidden " rg-cmd))
          projectile-git-command projectile-generic-command))))
#+end_src

** Treemacs

#+begin_src emacs-lisp
(use-package treemacs
  :init
  ;; https://github.com/Alexander-Miller/treemacs#configuration
  (setq treemacs-read-string-input 'from-minibuffer ; 이맥스 미니버퍼
        treemacs-litter-directories '("/vendor" "/node_modules")
        treemacs-no-png-images t))

(use-package treemacs-evil
  :after (treemacs evil))

(use-package treemacs-projectile
  :after (treemacs projectile))
#+end_src

** Org

#+begin_src emacs-lisp
(use-package org
  :init
  (setq org-startup-folded t
        org-adapt-indentation nil
        org-src-fontify-natively t
        org-src-tab-acts-natively t
        org-confirm-babel-evaluate nil
        org-edit-src-content-indentation 0
        org-imenu-depth 3
        org-log-done 'time
        org-babel-default-header-args:sh `((:noweb . "yes") (:results . "output"))
        org-babel-go-command "GO111MODULE=off go"
        org-agenda-window-setup 'current-window
        org-agenda-start-with-log-mode '(closed)
        org-agenda-tags-column 0
        org-todo-keywords '((sequence "TODO(t)" "INPROGRESS(i)" "PAUSED(p)" "|"
                                      "DONE(d)" "CANCELED(c)"))
        org-agenda-files '("~/org/inbox.org"
                           "~/org/gtd.org"
                           "~/org/tickler.org")
        org-refile-targets '(("~/org/gtd.org" :maxlevel . 1)
                             ("~/org/someday.org" :level . 1)
                             ("~/org/tickler.org" :maxlevel . 2))
        org-capture-templates '(("t" "TODO [inbox]" entry
                                 (file+headline "~/org/inbox.org" "TODOs")
                                 "* TODO %i%?")
                                ("T" "Tickler" entry
                                 (file "~/org/tickler.org")
                                 "* %i%? \n %U"))
        org-tag-alist '(("crypt" . ?c) ("@home" . ?h) ("@office" . ?o))
        org-babel-default-header-args '((:eval . "never-export"))
        org-html-postamble nil
        org-html-doctype "html5"
        org-html-html5-fancy t
        org-html-validation-link nil
        org-html-head-include-default-style t)

  :config
  (defun my/get-theme-colors-for-org-html ()
    (let* ((bg-color (face-background 'default))
           (fg-color (face-foreground 'default))
           (comment-color (face-foreground 'font-lock-comment-face))
           (keyword-color (face-foreground 'font-lock-keyword-face))
           (string-color (face-foreground 'font-lock-string-face)))
      (format "<style>
pre.src {
  background-color: %s !important;
  color: %s !important;
  border: 1px solid %s;
  padding: 1rem;
  border-radius: 6px;
}
.org-comment { color: %s !important; }
.org-keyword { color: %s !important; }
.org-string { color: %s !important; }
</style>"
              bg-color fg-color comment-color
              comment-color keyword-color string-color)))

  (defun my/update-org-html-theme ()
    (setq org-html-head (my/get-theme-colors-for-org-html)))

  (defun my/org-mode-after-setup ()
    "Org Mode가 완전하게 로딩된 이후에 실행할 설정"
    (require 'org-tempo)
    (setq org-structure-template-alist
          (append org-structure-template-alist
                  '(("sh" . "src sh")
                    ("u"  . "src plantuml :file ?.png :cmdline -charset UTF-8")
                    ("t"  . "src typescript :cmdline -t es6"))))

    (org-babel-do-load-languages
     'org-babel-load-languages
     '((python . t)
       (ditaa . t)
       (java . t)
       (go . t)
       (js . t)
       (C . t)
       (shell . t)
       (plantuml . t)))
    ;; org-mode 내에서 *bold* 등의 강조 문법 인식 경계 설정
    (setcar (nthcdr 2 org-emphasis-regexp-components) " \t\n,")

    ;; 테마 변경 시 적용
    (add-hook 'enable-theme-functions
              (lambda (theme) (my/update-org-html-theme)))
    (advice-add 'disable-theme
                :after (lambda (&rest _) (my/update-org-html-theme))))
  :hook (org-mode . my/org-mode-after-setup)
  :custom
  (org-emphasis-alist org-emphasis-alist)
  :custom-face
  (org-document-title ((t (:height 1.0))))
  (org-level-1 ((t (:height 1.0))))
  (org-level-2 ((t (:height 1.0))))
  (org-level-3 ((t (:height 1.0))))
  (org-level-4 ((t (:height 1.0))))
  (org-level-5 ((t (:height 1.0))))
  (org-level-6 ((t (:height 1.0))))
  (org-level-7 ((t (:height 1.0))))
  (org-level-8 ((t (:height 1.0)))))

(use-package epa-file
  :after (org)
  :straight (:type built-in)
  :autoload epa-file-enable)

(use-package org-crypt
  :after (org)
  :straight (:type built-in)
  :config
  (setq epa-file-select-keys nil
        epg-pinentry-mode 'loopback
        auth-sources '((:source "~/.authinfo.gpg"))
        org-crypt-key nil
        org-tags-exclude-from-inheritance (quote ("crypt")))
  ;; 암호 항목에 대한 tangle 내보내기 문제 해결
  (org-crypt-use-before-save-magic)
  (defun my/reveal-and-move-back ()
    (org-reveal)
    (goto-char my/old-point))
  (defun my/org-reveal-after-save-on ()
    (setq my/old-point (point))
    (add-hook 'after-save-hook 'my/reveal-and-move-back))
  (defun my/org-reveal-after-save-off ()
    (remove-hook 'after-save-hook 'my/reveal-and-move-back))
  (add-hook 'org-babel-pre-tangle-hook 'my/org-reveal-after-save-on)
  (add-hook 'org-babel-post-tangle-hook 'my/org-reveal-after-save-off))

(use-package ox-gfm
  :after (org)
  :config
  (defun my/org-gfm-format-toc (headline info)
    "Return an appropriate table of contents entry for HEADLINE."
    (let* ((title (org-export-data (org-element-property :title headline) info))
           (level (org-export-get-relative-level headline info))
           (indent (concat (make-string (* (1- level) 2) ?\s)))
           ;; GitHub style
           (anchor (or (org-element-property :CUSTOM_ID headline)
                       (replace-regexp-in-string
                        "[^a-z0-9-]" ""
                        (replace-regexp-in-string
                         " " "-"
                         (downcase title))))))
      (concat indent "- [" title "](#" anchor ")")))

  (advice-add 'org-gfm-format-toc
              :override #'my/org-gfm-format-toc))

(use-package org-tree-slide
  :after (org))

(use-package org-super-agenda
  :after org-agenda
  :config
  (org-super-agenda-mode))

(use-package org-journal
  :after org
  :custom
  (org-journal-dir "~/org/journal/")
  (org-journal-search-result-date-format "%Y-%m-%d, %A")
  (org-journal-date-format "%Y-%m-%d, %A")
  (org-journal-time-format "<%Y-%m-%d %a %H:%M>")
  (org-journal-file-format "%Y/%m/%Y%m%d.org")
  (org-journal-file-type 'weekly)
  (org-journal-file-header "#+TITLE: %Y.%m Journal\n#+OPTIONS: toc:nil ^:nil \\n:t")
  (org-journal-enable-agenda-integration t)
  :custom-face
  (calendar-today ((t (:background "#CC9393" :underline t))))
  (org-journal-calendar-entry-face ((t (:foreground "#BDE0F3" :slant italic)))))
#+end_src

** Translate

#+begin_src emacs-lisp
(use-package google-translate
  :init
  (setq google-translate-default-source-language "en"
        google-translate-default-target-language "ko"
        google-translate-output-destination nil)
  (defun google-translate--search-tkk ()
    "https://github.com/atykhonov/google-translate/issues/137"
    (list 430675 2721866130)))
#+end_src

** Elfeed

#+begin_src emacs-lisp
(use-package elfeed
  :after evil
  :init
  (setq-default elfeed-search-filter "@1-month-ago +unread "
                elfeed-curl-extra-arguments '("--insecure")
                elfeed-show-entry-switch 'switch-to-buffer)
  :config
  (defun my/elfeed-toggle-flagged ()
    "Toggle flagged tag for current entry"
    (interactive)
    (let ((entry (elfeed-search-selected :single)))
      (if (elfeed-tagged-p 'flagged entry)
          (elfeed-untag entry 'flagged)
        (elfeed-tag entry 'flagged))
      (elfeed-search-update-entry entry)))

  (defun my/elfeed-show-flagged ()
    "Show only flagged entries"
    (interactive)
    (elfeed-search-set-filter "+flagged"))

  (defun my/elfeed-export-flagged ()
    "Export flagged entries to org file with tags"
    (interactive)
    (let ((exported-count 0)
          (existing-links (make-hash-table :test 'equal)))

      (with-current-buffer (find-file-noselect "~/org/feed.org")
        (when (= (buffer-size) 0)
          (insert "#+TITLE: Feed Flagged Entries\n\n"))

        ;; 기존 링크들 수집 (중복 방지용)
        (save-excursion
          (goto-char (point-min))
          (while (re-search-forward "\\* \\[\\[\\([^]]+\\)\\]" nil t)
            (puthash (match-string 1) t existing-links)))

        (goto-char (point-max))

        (with-elfeed-db-visit (entry feed)
          (when (and (elfeed-tagged-p 'flagged entry)
                     (not (gethash (elfeed-entry-link entry) existing-links)))
            (let* ((entry-tags (elfeed-entry-tags entry))
                   (exclude-tags '(unread flagged))
                   (user-tags (cl-remove-if (lambda (tag)
                                              (memq tag exclude-tags))
                                            entry-tags))
                   (org-tags-string (if user-tags
                                        (concat ":"
                                                (mapconcat (lambda (tag)
                                                             (symbol-name tag))
                                                           user-tags ":")
                                                ":")
                                      "")))
              (insert (format "* [[%s][%s]] %s\n"
                              (elfeed-entry-link entry)
                              (elfeed-entry-title entry)
                              org-tags-string))
              (insert (format ":PROPERTIES:\n:DATE: %s\n:FEED: %s\n:ELFEED_TAGS: %s\n:END:\n\n"
                              (format-time-string "%Y-%m-%d" (elfeed-entry-date entry))
                              (elfeed-feed-title feed)
                              (mapconcat 'symbol-name entry-tags " ")))
              (setq exported-count (1+ exported-count)))))

        (save-buffer)
        (if (> exported-count 0)
            (message "Appended %d new entries to ~/org/feed.org" exported-count)
          (message "No new entries to export")))))
  :config
  (evil-define-key 'normal elfeed-search-mode-map
    (kbd "f") #'my/elfeed-toggle-flagged
    (kbd "F") #'my/elfeed-show-flagged
    (kbd "E") #'my/elfeed-export-flagged))

(use-package elfeed-org
  :after (elfeed org)
  :init
  (setq rmh-elfeed-org-files (list "~/.emacs.d/elfeed.org"))
  :config
  (elfeed-org))

(use-package elfeed-summary
  :after elfeed-org
  :config
  (setq elfeed-summary-settings
        '((auto-tags
           (:original-order . t)
           (:max-level . 1))))

  ;; 현재 창에서 열리도록 설정
  (setq display-buffer-alist
        (append display-buffer-alist
                '(("\\*elfeed-summary\\*"
                   (display-buffer-same-window))))))
#+end_src

** Shell

#+begin_src emacs-lisp
(use-package eshell
  :straight (:type built-in)
  :init
  (defalias 'my/sh 'eshell))

(use-package vterm
  :when (memq window-system '(mac ns x pgtk))
  :demand t
  :init
  (setq vterm-always-compile-module t
        vterm-timer-delay 0.01)
  :hook
  ((vterm-mode . my/disable-line-numbers-mode))
  :config
  (advice-add 'vterm
              :after (lambda (buffer)
                       (when-let ((proc (get-buffer-process buffer)))
                         (set-process-query-on-exit-flag proc nil))))
  (defalias 'my/sh 'vterm))

(use-package zsh-mode
  :straight nil
  :after sh-script
  :commands zsh-mode
  :mode (("\\.zsh\\'"  . zsh-mode)
         ("zshrc\\'"   . zsh-mode)
         ("zshenv\\'"  . zsh-mode))
  :init
  (define-derived-mode zsh-mode sh-mode "Zsh"
    "Major mode for editing Zsh shell scripts."
    (sh-set-shell "zsh")

    (font-lock-add-keywords
     nil
     '(("\\<\\(autoload\\|compdef\\|zstyle\\|bindkey\\|setopt\\|unsetopt\\)\\>"
        1 font-lock-keyword-face)))

    (setq imenu-generic-expression
          '(("Functions" "^\\s-*function\\s-+\\([A-Za-z_/][A-Za-z_0-9/:-]*\\)" 1)
            ("Functions" "^\\s-*\\([A-Za-z_/][A-Za-z_0-9/:-]*\\)\\s-*()" 1)
            ("Variables" "^\\s-*\\([A-Za-z_][A-Za-z_0-9]*\\)=" 1)
            ("Local Variables" "^\\s-*local\\s-+\\([A-Za-z_][A-Za-z_0-9]*\\)" 1)
            ("Autoload" "^\\s-*autoload\\s-+\\([A-Za-z_/][A-Za-z_0-9/:-]*\\)" 1)
            ("Aliases" "^\\s-*alias\\s-+\\([A-Za-z_/][A-Za-z_0-9/:-]*\\)" 1)))))

(defun my/switch-sh (n)
  "Shell 버퍼 변경"
  (let ((buffer-name (format "sh<%d>" n)))
    (cond ((get-buffer buffer-name)
           (switch-to-buffer buffer-name))
          (t (my/sh buffer-name)
             (rename-buffer buffer-name)))))
#+end_src

** Eglot

#+begin_src emacs-lisp
(use-package eglot
  :demand t
  :init
  (setq-default eglot-workspace-configuration
                '((:gopls . ((staticcheck . t)
                             (matcher . "CaseSensitive")))))
  :config
  (evil-collection-define-key 'normal 'eglot-mode-map "gr" 'xref-find-references)

  :bind
  (:map eglot-mode-map
        ("C-c l g d" . xref-find-definitions)
        ("C-c l g r" . xref-find-references)
        ("C-c l g i" . eglot-find-implementation)
        ("C-c l r"  . eglot-rename)
        ("C-c l o" . eglot-code-action-organize-imports)
        ("C-c l h" . eldoc)))

(use-package consult-eglot
  :after (eglot consult))
#+end_src

** DB

#+begin_src emacs-lisp
(setq sql-postgres-login-params '(server port user database)
      sql-mysql-login-params '(server port user database)
      sql-sqlite-login-params '(server port user database)
      sql-db2-login-params '(server port user database)
      sql-oracle-login-params '(server port user database)
      sql-ms-login-params '(server port user database))
#+end_src

** Lisp

#+begin_src emacs-lisp
(use-package paredit
  :hook
  ((ielm-mode
    lisp-mode
    emacs-lisp-mode
    lisp-interaction-mode
    scheme-mode) . paredit-mode))
#+end_src

** Go

유틸리티

#+begin_src sh
go install github.com/klauspost/asmfmt/cmd/asmfmt@latest
go install github.com/go-delve/delve/cmd/dlv@latest
go install github.com/kisielk/errcheck@latest
go install github.com/davidrjenni/reftools/cmd/fillstruct@master
go install github.com/rogpeppe/godef@latest
go install golang.org/x/tools/cmd/goimports@master
go install github.com/mgechev/revive@latest
go install golang.org/x/tools/gopls@latest
go install github.com/golangci/golangci-lint/cmd/golangci-lint@latest
go install honnef.co/go/tools/cmd/staticcheck@latest
go install github.com/fatih/gomodifytags@latest
go install golang.org/x/tools/cmd/gorename@master
go install github.com/jstemmer/gotags@master
go install golang.org/x/tools/cmd/guru@master
go install github.com/josharian/impl@main
go install honnef.co/go/tools/cmd/keyify@master
go install github.com/fatih/motion@latest
go install github.com/koron/iferr@master
#+end_src

#+begin_src emacs-lisp
(use-package go-mode
  :after (eglot project)
  :mode
  ("\\.go\\'" "\\.go\\.bak\\'")
  :hook
  ((go-mode . eglot-ensure)
   (go-mode . hs-minor-mode)
   (go-mode . my/go-mode-setup))
  :config
  (defun my/go-compile-command ()
    (let* ((name (file-name-nondirectory (buffer-file-name)))
           (cmd (if (string-match "_test\\.go$" name) "test" "run")))
      ;; (format "go %s -v %s" cmd name)
      (format "go %s -v ." cmd)))

  (defun my/go-compile ()
    "Go 파일에 맞는 컴파일 명령 실행"
    (interactive)
    (setq compile-command (my/go-compile-command))
    (call-interactively 'compile))

  (defun my/go-mode-setup()
    (setq tab-width 2)
    (when (eq major-mode 'go-mode)
      (local-set-key (kbd "C-c C-c") 'my/go-compile)))

  (defun my/project-find-go-module (dir)
    (when-let ((root (locate-dominating-file dir "go.mod")))
      (cons 'go-module root)))

  (cl-defmethod project-root ((project (head go-module)))
    (cdr project))

  (defun my/eglot-before-save()
    (interactive)
    (when (eq major-mode 'go-mode)
      (eglot-format-buffer)
      (call-interactively 'eglot-code-action-organize-imports)))

  ;; 전역 hook 설정 추가
  (add-hook 'project-find-functions #'my/project-find-go-module)
  (add-hook 'before-save-hook #'my/eglot-before-save))

(use-package go-tag
  :after (go-mode))

(use-package ob-go
  :after (go-mode))
#+end_src

** Java

#+begin_src emacs-lisp
;; (use-package eglot-java
;;   :config
;;   ;; (setq lombok-jar-path (expand-file-name "bin/lombok-1.18.jar" user-emacs-directory))
;;   ;; (add-to-list 'eglot-java-eclipse-jdt-args (concat "-javaagent:" lombok-jar-path))
;;   (add-hook 'java-mode-hook 'eglot-java-mode))
#+end_src

** File Syntax
#+begin_src emacs-lisp
(when (image-type-available-p 'svg)
  (require 'svg))

;; W : 너비를 창에 맞춤 (fit width)
;; H : 높이를 창에 맞춤 (fit height)
;; P : 페이지 전체를 창에 맞춤 (fit page)
;; 0 : 원래 크기로 리셋
(use-package pdf-tools
  :magic
  ("%PDF" . pdf-view-mode)
  :config
  (pdf-tools-install :no-query)
  :hook
  ((pdf-view-mode . (lambda() (display-line-numbers-mode -1)))))

(use-package json-mode
  :hook
  ((json-mode . highlight-indentation-mode))
  :config
  (setq js-indent-level tab-width))

(use-package yaml-mode
  :hook
  ((yaml-mode . highlight-indentation-mode)))

(use-package plantuml-mode
  :config
  (let ((jar-file "~/.emacs.d/bin/plantuml.jar"))
    (setq plantuml-default-exec-mode 'jar
          plantuml-jar-path jar-file
          org-plantuml-jar-path jar-file)))

(use-package k8s-mode
  :config
  (setq k8s-indent-offset nil
        k8s-site-docs-url "https://kubernetes.io/docs/reference/generated/kubernetes-api/")
  (add-to-list 'org-src-lang-modes '("k8s" . yaml)))

(use-package markdown-mode
  :config
  (setq markdown-command "multimarkdown"))

(use-package dockerfile-mode
  :mode
  (("Dockerfile.*\\'" . dockerfile-mode)))

(use-package rust-mode)
(use-package adoc-mode)
(use-package js2-mode)
(use-package groovy-mode)
(use-package vimrc-mode)
#+end_src

** Mail

#+begin_src emacs-lisp :tangle (if (eq system-type 'windows-nt) "no" "yes")
(use-package mu4e
  :straight ( :type git
              :host github
              :repo "djcb/mu"
              :branch "v1.12.12"
              :files ("build/mu4e/*.el"))
  :config
  ;; maildir 첫 번째 경로를 context로 사용
  (add-to-list 'mu4e-header-info-custom
               '(:context . ( :name "Context"
                              :shortname "Ctx"
                              :help "Message context from maildir"
                              :function
                              (lambda (msg)
                                (let ((maildir (mu4e-message-field msg :maildir)))
                                  (if maildir
                                      (let* ((clean-path (replace-regexp-in-string "^/" "" maildir))
                                             (paths (split-string clean-path "/")))
                                        (if paths (car paths) ""))
                                    ""))))))

  (setq read-mail-command 'mu4e
        mail-user-agent 'mu4e-user-agent
        mu4e-get-mail-command "mbsync -a"
        mu4e-change-filenames-when-moving t
        mu4e-confirm-quit nil
        mu4e-context-policy 'pick-first
        mu4e-completing-read-function 'completing-read
        mu4e-search-full t
        mu4e-search-include-related nil
        mu4e-eldoc-support t
        mu4e-notification-support t
        mu4e-update-interval (* 5 60)
        mu4e-attachment-dir "~/Downloads/"
        mu4e-maildir-shortcuts '(("/Inbox" . ?i))
        mu4e-headers-visible-lines 20
        mu4e-headers-visible-columns 80
        mu4e-headers-visible-lines 10
        mu4e-headers-date-format "%Y-%m-%dT%H:%M"
        mu4e-headers-fields '((:human-date . 18)
                              (:flags      . 6)
                              (:context    . 8)
                              (:from       . 22)
                              (:subject    . nil))))

(use-package smtpmail
  :straight (:type built-in)
  :init
  (setq message-citation-line-format "%N @ %Y-%m-%dT%H:%M :\n"
        message-citation-line-function 'message-insert-formatted-citation-line
        message-send-mail-function 'smtpmail-send-it
        message-kill-buffer-on-exit t
        starttls-use-gnutls t
        smtpmail-starttls-credentials '(("smtp.gmail.com" 587 nil nil))
        smtpmail-default-smtp-server "smtp.gmail.com"
        smtpmail-smtp-server "smtp.gmail.com"
        smtpmail-smtp-service 587))

;; Org export HTML 메일 발송
(use-package org-mime
  :hook
  (org-mime-html . (lambda ()
                     (org-mime-change-element-style
                      "pre" "border: 1px solid #eee;"))))
#+end_src

* Binding

[[https://stackoverflow.com/questions/683425/globally-override-key-binding-in-emacs][설정 파일 끝에 유지]]

#+begin_src emacs-lisp
(defvar my/key-map (make-keymap)
  "추가적인 keymap 설정")

(define-minor-mode my/key-mode
  "전역적인 커스텀 단축키 설정 모드"
  :init-value t
  :keymap my/key-map
  :lighter " My")

(my/key-mode t)
#+end_src

** Bindings for Mode

#+begin_src emacs-lisp
(keymap-set emacs-lisp-mode-map "C-c C-c" 'eval-defun)
(keymap-set lisp-interaction-mode-map "C-c C-c" 'eval-defun)

(keymap-set minibuffer-local-map "C-l" 'my/minibuffer-backward-kill-word)

(add-hook 'ielm-mode-hook
          (lambda ()
            (keymap-set my/key-map "C-j" 'ielm-return)))
#+end_src

** Bindings for Imenu

#+begin_src emacs-lisp
(keymap-set my/key-map "C-'" 'imenu-list-smart-toggle)
#+end_src

** Bindings for [[https://github.com/bbatsov/projectile][Projectile]]

#+begin_src emacs-lisp
(keymap-set my/key-map "C-c p" 'projectile-command-map)
#+end_src

** Bindings for Completion

#+begin_src emacs-lisp
(keymap-set my/key-map "C-s" 'consult-line)
(keymap-set my/key-map "M-y" 'consult-yank-pop)
(keymap-set my/key-map "C-x b" 'consult-buffer)
;; (keymap-set my/key-map "C-c b" 'consult-bookmark) mu4e와 충돌
(keymap-set my/key-map "C-c i" 'consult-imenu)
(keymap-set my/key-map "C-c r" 'consult-recent-file)
(keymap-set my/key-map "C-h M" 'consult-minor-mode-menu)
#+end_src

** Bindings for [[https://orgmode.org][Org]]

#+begin_src emacs-lisp
(keymap-set my/key-map "C-c a" 'org-agenda)
(keymap-set my/key-map "C-c c" 'org-capture)
(keymap-set my/key-map "C-c n"
            (lambda () (interactive) (org-agenda nil "n")))

(keymap-set my/key-map "C-c j" 'org-journal-open-current-journal-file)
(keymap-set my/key-map "C-c J" 'org-journal-new-entry)
#+end_src

** Bindings for Shell

#+begin_src emacs-lisp
(dolist (n (number-sequence 1 9))
  (keymap-set my/key-map (concat "M-" (int-to-string n))
              (lambda () (interactive) (my/switch-sh n))))
(keymap-set my/key-map "M-`"
            (lambda () (interactive) (my/switch-sh 10)))
#+end_src

** Bindings for Base

#+begin_src emacs-lisp
(keymap-set my/key-map "C-SPC" 'toggle-input-method)
(keymap-set my/key-map "C-c e" 'treemacs)
(keymap-set my/key-map "C-M-f" 'toggle-frame-fullscreen)
(keymap-set my/key-map "C-x k" 'my/kill-this-buffer-unless-scratch)
(keymap-set my/key-map "C-c o" 'olivetti-mode)
(keymap-set my/key-map "C-c m" 'magit-status)
(keymap-set my/key-map "C->" 'er/expand-region)
(keymap-set my/key-map "C-<" 'er/contract-region)
(keymap-set my/key-map "C-c T" 'google-translate-at-point)

(keymap-set my/key-map "<f5>" (lambda () (interactive) (find-file "~/org/inbox.org")))
(keymap-set my/key-map "<f6>" (lambda () (interactive) (find-file "~/org/ref.org")))
(keymap-set my/key-map "<f7>" 'mu4e)
(keymap-set my/key-map "<f8>" 'elfeed-summary)
(keymap-set my/key-map "<f9>" (lambda () (interactive) (org-agenda t "n")))
#+end_src
